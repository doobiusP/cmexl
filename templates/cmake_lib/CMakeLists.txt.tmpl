cmake_minimum_required(VERSION 3.16)
message(VERBOSE "[DOOBIUS] Now processing ${CMAKE_CURRENT_LIST_FILE}")

# Enable Hot Reload for MSVC compilers if supported
if(MSVC AND POLICY CMP0141)
  cmake_policy(SET CMP0141 NEW)
  set(CMAKE_MSVC_DEBUG_INFORMATION_FORMAT
    "$<$<CONFIG:Debug,RelWithDebInfo>:ProgramDatabase>"
    "$<$<CONFIG:Debug,RelWithDebInfo>:EditAndContinue>")
endif()

# =============== Settings =============== #
include(cmake/DoobiusOptions.cmake)
# =============== Vcpkg =============== #
if(DOOBIUS_USE_VCPKG)
  include(cmake/DoobiusFeatures.cmake)
  if(DOOBIUS_VERBOSE)
    message(STATUS "[DOOBIUS][VCPKG] Triplet = ${VCPKG_TARGET_TRIPLET}")
    message(STATUS "[DOOBIUS][VCPKG] Toolchain = ${CMAKE_TOOLCHAIN_FILE}")
    message(STATUS "[DOOBIUS][VCPKG] Chainload Toolchain = ${VCPKG_CHAINLOAD_TOOLCHAIN_FILE}")
    message(STATUS "[DOOBIUS][VCPKG] Features = ${VCPKG_MANIFEST_FEATURES}")

    message(STATUS "[DOOBIUS][VCPKG] Build Tests = ${DOOBIUS_BUILD_TESTS}")
    message(STATUS "[DOOBIUS][VCPKG] Override and build test for package manager = ${DOOBIUS_CHECK_PKG_MANAGER}")

    message(STATUS "[DOOBIUS][VCPKG] Use addr2line = ${DOOBIUS_USE_ADDR2LINE}")
    message(STATUS "[DOOBIUS][VCPKG] Use backtrace = ${DOOBIUS_USE_BACKTRACE}")
  endif()
endif()
# =============== Conan =============== #
if(DOOBIUS_USE_CONAN)
  message(FATAL_ERROR "[DOOBIUS] Not supported yet") #TODO: Support Conan
endif()

# =============== Project Init =============== #
include(cmake/DoobiusVersionDetails.cmake)

message(STATUS "[CMEXL] starting vcpkg")
project(DoobiusGameLibrary VERSION ${DOOBIUS_VER} LANGUAGES C CXX)
message(STATUS "[CMEXL] vcpkg finished")

set(CMAKE_CXX_STANDARD 20)
set(CMAKE_CXX_STANDARD_REQUIRED ON)
set(CMAKE_CXX_EXTENSIONS OFF)

# =============== Config Check =============== #
if((NOT GENERATOR_IS_MULTI_CONFIG) AND (NOT CMAKE_BUILD_TYPE))
  message(WARNING "[DOOBIUS] CMAKE_BUILD_TYPE not explicitly set. Using Debug as default...")
  set(CMAKE_BUILD_TYPE Debug CACHE STRING "" FORCE)
endif()

set(ALLOWED_BUILD_TYPES Debug Release RelWithDebInfo MinSizeRel)
if(NOT CMAKE_BUILD_TYPE IN_LIST ALLOWED_BUILD_TYPES)
  message(FATAL_ERROR
    "[DOOBIUS] Invalid build type: ${CMAKE_BUILD_TYPE}."
    "Allowed values are: ${ALLOWED_BUILD_TYPES}"
  )
endif()

include(cmake/DoobiusDefaults.cmake)

if(NOT DOOBIUS_CHECK_PKG_MANAGER)
  set(DOOBIUS_BOOST_STACKTRACE_IMPORT stacktrace_basic)
  set(DOOBIUS_NEED_LIBDL OFF)
  set(DOOBIUS_NEED_DBGENG OFF)

  function(_doobius_find_addr2line OUTVAR)
    if(DOOBIUS_USING_MINGW)
      set(_names x86_64-w64-mingw32-addr2line)
      if(DOOBIUS_32BIT)
        list(APPEND _names i686-w64-mingw32-addr2line)
      endif()
      list(APPEND _names addr2line)
    else()
      set(_names addr2line)
    endif()

    find_program(_bin NAMES ${_names})
    if(NOT _bin)
      message(FATAL_ERROR "[DOOBIUS] Cannot find addr2line. Re-run CMake with --debug-find to diagnose.")
    endif()
    if(DOOBIUS_VERBOSE)
      message(STATUS "[DOOBIUS] Using addr2line: ${_bin}")
    endif()
    set(${OUTVAR} "${_bin}" PARENT_SCOPE)
  endfunction()

  if(WIN32)
    set(DOOBIUS_BOOST_STACKTRACE_IMPORT stacktrace_windbg)
    set(DOOBIUS_NEED_DBGENG ON)
    if(DOOBIUS_USE_BACKTRACE OR DOOBIUS_USE_ADDR2LINE)
      message(WARNING "[DOOBIUS] Disabling backtrace and/or addr2line on windows")
    endif()
    set(DOOBIUS_USE_BACKTRACE OFF)
    set(DOOBIUS_USE_ADDR2LINE OFF)
  else()
    set(DOOBIUS_NEED_LIBDL ON)

    if(DOOBIUS_USE_BACKTRACE)
      set(DOOBIUS_BOOST_STACKTRACE_IMPORT stacktrace_backtrace)
    elseif(DOOBIUS_USE_ADDR2LINE)
      _doobius_find_addr2line(DOOBIUS_ADDR2LINE_BINARY)
      set(DOOBIUS_BOOST_STACKTRACE_IMPORT stacktrace_addr2line)
    endif()

  endif()

  find_package(Boost CONFIG REQUIRED COMPONENTS
    assert
    format
    json
    log
    container
    ${DOOBIUS_BOOST_STACKTRACE_IMPORT}
  )
else()
  find_package(ZLIB REQUIRED)
endif()

add_subdirectory(src)
